buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'com.google.guava:guava:26.0-jre'
  }
}

import static com.google.common.base.Preconditions.checkState

plugins {
  id 'maven-publish'
}

dependencies {
  compile 'org.hibernate:hibernate-core:5.4.1.Final'
  compile 'org.hibernate:hibernate-hikaricp:5.4.1.Final'

  compile 'com.zaxxer:HikariCP:3.3.1'
  runtimeOnly 'org.postgresql:postgresql:42.2.5'

  compile 'org.reflections:reflections:0.9.11'

  testCompile 'org.testcontainers:testcontainers:1.10.6'
  testCompile 'org.testcontainers:postgresql:1.10.6'

  testCompile project(':testutils')
}

tasks.jar.archiveClassifier = 'all'

task schemaSourcesJar(type: Jar) {
  archiveBaseName = 'schema'
  archiveClassifier = 'sources'
  from(sourceSets.main.allJava) {
    include 'demoschema/orm/**'
  }
}

task schemaJar(type: Jar) {
  archiveBaseName = 'schema'
  archiveClassifier = 'orm'
  from(sourceSets.main.output) {
    include 'demoschema/orm/**'
  }
}

artifacts {
  archives schemaJar
}

publishing {
  repositories {
    maven {
      url project.hasProperty('maven_repo') ? "$maven_repo" : 'file:///tmp'
    }
  }
  publications {
    schemaOrmPublication(MavenPublication) {
      groupId 'demoschema'
      artifactId 'demoschema'
      version '2'

      from components.java
      artifact schemaJar
      artifact schemaSourcesJar
    }
  }
}

def INTEGRATION_TESTS = ["**/*IntegrationTest.*"]

test {
  systemProperties project.getProperties().subMap('actual_only')
}

// Non-hermetic tests that require a database.
task integration_test(type: Test) {
  // Pass any connection parameters on Gradle command line to test.
  // JdbcUrl example: jdbc:postgresql://host:5432/dbName
  // Note: property names are intentionally different from those used for flyway
  systemProperties project.getProperties().subMap('jdbcUrl', 'dbUser', 'dbPassword')

  include INTEGRATION_TESTS

  doFirst {
    checkState(
        project.hasProperty('jdbcUrl'),
        "Missing command line flags for jdbcUrl, dbUser, or dbPassword")
  }
}
