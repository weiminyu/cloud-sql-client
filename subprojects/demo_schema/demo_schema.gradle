import com.google.common.base.Splitter
import com.google.common.collect.ImmutableList
import org.flywaydb.core.api.Location

buildscript {
    dependencies {
        classpath 'com.google.guava:guava:26.0-jre'
        classpath 'org.postgresql:postgresql:42.2.5'
    }
}

plugins {
    id "org.flywaydb.flyway" version "5.2.4"
}

flyway {
    ext.parseStringList = { comma_separated_string ->
        return ImmutableList.copyOf(
                Splitter.on(',').omitEmptyStrings().trimResults().split(comma_separated_string));
    }

    def host = getPropertyOrDefault('db_host', 'localhost')
    def port = getPropertyOrDefault('db_port', '5432')
    def dbname = getPropertyOrDefault('db_name', 'nomulus')

    url = String.format('jdbc:postgresql://%s:%s/%s', host, port, dbname)
    user = getPropertyOrDefault('db_user', 'postgres')
    password = findProperty('db_password')
    schemas = parseStringList(getPropertyOrDefault('db_schemas', 'public'))

    // Must instantiate Location here. Using string as in online doc breaks
    // the classpath for the whole project.
    locations = new Location("flywaydb/migration")
}

dependencies {
    compile 'org.hibernate:hibernate-core:5.4.1.Final'
    compile 'org.hibernate:hibernate-hikaricp:5.4.1.Final'

    compile 'com.zaxxer:HikariCP:3.3.1'
    runtimeOnly 'org.postgresql:postgresql:42.2.5'

    compile 'org.reflections:reflections:0.9.11'

    compile 'org.flywaydb:flyway-core:5.2.4'

    testCompile 'org.testcontainers:testcontainers:1.10.6'
    testCompile 'org.testcontainers:postgresql:1.10.6'
}

def INTEGRATION_TESTS = [ "**/*IntegrationTest.*" ]

test {
    exclude INTEGRATION_TESTS
}

// Non-hermetic tests that require a database.
task integration_test(type: Test) {
    // Pass any connection parameters on Gradle command line to test.
    // JdbcUrl example: jdbc:postgresql://host:5432/dbName
    // Note: property names are intentionally different from those used for flyway
    systemProperties project.getProperties().subMap('jdbcUrl', 'dbUser', 'dbPassword')

    include INTEGRATION_TESTS
}
