buildscript {
    dependencies {
        classpath 'org.postgresql:postgresql:42.2.5'
    }
}

plugins {
    id 'org.liquibase.gradle' version '2.0.1'
    id 'java-library'
}

dependencies {
    liquibaseRuntime 'org.liquibase:liquibase-core:3.6.1'
    liquibaseRuntime 'org.postgresql:postgresql:42.2.5'
    liquibaseRuntime project(':demo_schema')

    // Needed for task dependency declarations at the end of this script
    compile project(':demo_schema')

    compile 'org.liquibase:liquibase-core:3.6.1'

    testRuntimeOnly 'org.postgresql:postgresql:42.2.5'
    testCompile 'org.testcontainers:postgresql:1.10.6'
    testCompile 'org.testcontainers:testcontainers:1.10.6'
}

liquibase {

    activities {
        main {
            def format = project.ext.getPropertyOrDefault('change_format', 'xml')
            // ChangeLog is from :demo_schema
            changeLogFile "liquibase/migration/${format}/ChangeLog.${format}"
            url String.format('jdbc:postgresql://%s:%s/%s',
                    project.ext.getPropertyOrDefault('db_host', 'localhost'),
                    project.ext.getPropertyOrDefault('db_port', '5432'),
                    project.ext.getPropertyOrDefault('db_name', 'nomulus'))
            username project.ext.getPropertyOrDefault('db_user', 'postgres')
            password project.findProperty('db_password')
        }
    }
}

// Make liquibase tasks rebuild :demo_schema on change.
tasks
        .findAll { task -> task.group.equals('Liquibase') }
        .collect { task -> task.dependsOn('buildNeeded') }
